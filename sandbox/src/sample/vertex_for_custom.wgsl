#define_import_path vertex_for_custom

#import bevy_pbr::{
    mesh_view_bindings::globals,
    pbr_functions::alpha_discard,

}

#import bevy_pbr::{
    pbr_types,
    mesh_bindings::mesh,
    mesh_functions,
    skinning,
    morph::{morph_position, morph_normal, morph_tangent},
    view_transformations::position_world_to_clip,
    forward_io::{Vertex, VertexOutput},
}

// https://github.com/bevyengine/bevy/blob/81127b3f619b830c95a24d67df0e3abaf7ad5ca3/crates/bevy_pbr/src/render/forward_io.wgsl#L141
struct MyVertexOutput {
    // This is `clip position` when the struct is used as a vertex stage output
    // and `frag coord` when used as a fragment stage input
    @builtin(position) position: vec4<f32>,
    @location(0) world_position: vec4<f32>,
    @location(1) world_normal: vec3<f32>,
#ifdef VERTEX_UVS_A
    @location(2) uv: vec2<f32>,
#endif
#ifdef VERTEX_UVS_B
    @location(3) uv_b: vec2<f32>,
#endif
#ifdef VERTEX_TANGENTS
    @location(4) world_tangent: vec4<f32>,
#endif
#ifdef VERTEX_COLORS
    @location(5) color: vec4<f32>,
#endif
#ifdef VERTEX_OUTPUT_INSTANCE_INDEX
    @location(6) @interpolate(flat) instance_index: u32,
#endif
#ifdef VISIBILITY_RANGE_DITHER
    @location(7) @interpolate(flat) visibility_range_dither: i32,
#endif

#ifdef MY_MESHES_ATTRIBUTE_TIME
    @location(10) time: f32,
#endif
}

fn to_my_vert_out(base: VertexOutput, time: f32) -> MyVertexOutput {
    var out: MyVertexOutput;
    out.position = base.position;
    out.world_position = base.world_position;
    out.world_normal = base.world_normal;
#ifdef VERTEX_UVS_A
    out.uv = base.uv;
#endif
#ifdef VERTEX_UVS_B
    out.uv_b = base.uv_b;
#endif
#ifdef VERTEX_TANGENTS
    out.world_tangent = base.world_tangent;
#endif
#ifdef VERTEX_COLORS
    out.color = base.color;
#endif
#ifdef VERTEX_OUTPUT_INSTANCE_INDEX
    out.instance_index = base.instance_index;
#endif
#ifdef VISIBILITY_RANGE_DITHER
    out.visibility_range_dither = base.visibility_range_dither;
#endif
#ifdef MY_MESHES_ATTRIBUTE_TIME
    out.time = time;
#endif

    return out;
}

fn to_vert_out(extended: MyVertexOutput) -> VertexOutput {
    var out: VertexOutput;
    out.position = extended.position;
    out.world_position = extended.world_position;
    out.world_normal = extended.world_normal;
#ifdef VERTEX_UVS_A
    out.uv = extended.uv;
#endif
#ifdef VERTEX_UVS_B
    out.uv_b = extended.uv_b;
#endif
#ifdef VERTEX_TANGENTS
    out.world_tangent = extended.world_tangent;
#endif
#ifdef VERTEX_COLORS
    out.color = extended.color;
#endif
#ifdef VERTEX_OUTPUT_INSTANCE_INDEX
    out.instance_index = extended.instance_index;
#endif
#ifdef VISIBILITY_RANGE_DITHER
    out.visibility_range_dither = extended.visibility_range_dither;
#endif

    return out;
}

#ifdef MORPH_TARGETS
// The instance_index parameter must match vertex_in.instance_index. This is a work around for a wgpu dx12 bug.
// See https://github.com/gfx-rs/naga/issues/2416
fn morph_vertex(
    vertex_in: Vertex, instance_index: u32,
) -> Vertex {
    var vertex = vertex_in;
    let first_vertex = mesh[instance_index].first_vertex_index;
    let vertex_index = vertex.index - first_vertex;

    let weight_count = bevy_pbr::morph::layer_count(instance_index);
    for (var i: u32 = 0u; i < weight_count; i ++) {
        let weight = bevy_pbr::morph::weight_at(i, instance_index);
        if weight == 0.0 {
            continue;
        }
        vertex.position += weight * morph_position(vertex_index, i, instance_index);
#ifdef VERTEX_NORMALS
        vertex.normal += weight * morph_normal(vertex_index, i, instance_index);
#endif
#ifdef VERTEX_TANGENTS
        vertex.tangent += vec4(weight * morph_tangent(vertex_index, i, instance_index), 0.0);
#endif
    }
    return vertex;
}
#endif

@vertex
fn vertex(
    vertex_no_morph: Vertex,
#ifdef MY_MESHES_ATTRIBUTE_TIME
    @location(10) time: f32,
#endif
) -> MyVertexOutput {
    var out: VertexOutput;

#ifdef MORPH_TARGETS
    var vertex = morph_vertex(vertex_no_morph, vertex_no_morph.instance_index);
#else
    var vertex = vertex_no_morph;
#endif

    let mesh_world_from_local = mesh_functions::get_world_from_local(vertex_no_morph.instance_index);

#ifdef SKINNED
    // Use vertex_no_morph.instance_index instead of vertex.instance_index to work around a wgpu dx12 bug.
    // See https://github.com/gfx-rs/naga/issues/2416 .
    var world_from_local = skinning::skin_model(
        vertex.joint_indices,
        vertex.joint_weights,
        vertex_no_morph.instance_index
    );
#else
    var world_from_local = mesh_world_from_local;
#endif

#ifdef VERTEX_NORMALS
#ifdef SKINNED
    out.world_normal = skinning::skin_normals(world_from_local, vertex.normal);
#else
    out.world_normal = mesh_functions::mesh_normal_local_to_world(
        vertex.normal,
        // Use vertex_no_morph.instance_index instead of vertex.instance_index to work around a wgpu dx12 bug.
        // See https://github.com/gfx-rs/naga/issues/2416
        vertex_no_morph.instance_index
    );
#endif
#endif

#ifdef VERTEX_POSITIONS
    out.world_position = mesh_functions::mesh_position_local_to_world(world_from_local, vec4<f32>(vertex.position, 1.0));

    out.position = position_world_to_clip(out.world_position.xyz);
#endif


#ifdef VERTEX_UVS_A
    out.uv = vertex.uv;
#endif
#ifdef VERTEX_UVS_B
    out.uv_b = vertex.uv_b;
#endif

#ifdef VERTEX_TANGENTS
    out.world_tangent = mesh_functions::mesh_tangent_local_to_world(
        world_from_local,
        vertex.tangent,
        // Use vertex_no_morph.instance_index instead of vertex.instance_index to work around a wgpu dx12 bug.
        // See https://github.com/gfx-rs/naga/issues/2416
        vertex_no_morph.instance_index
    );
#endif

#ifdef VERTEX_COLORS
    out.color = vertex.color;
#endif

#ifdef VERTEX_OUTPUT_INSTANCE_INDEX
    // Use vertex_no_morph.instance_index instead of vertex.instance_index to work around a wgpu dx12 bug.
    // See https://github.com/gfx-rs/naga/issues/2416
    out.instance_index = vertex_no_morph.instance_index;
#endif

#ifdef VISIBILITY_RANGE_DITHER
    out.visibility_range_dither = mesh_functions::get_visibility_range_dither_level(
        vertex_no_morph.instance_index, mesh_world_from_local[3]);
#endif

    var time_: f32 = 0.0;
#ifdef MY_MESHES_ATTRIBUTE_TIME
    time_ = time;
#endif
    return to_my_vert_out(out, time_);
}

